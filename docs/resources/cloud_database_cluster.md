---
page_title: "nexaa_cloud_database_cluster Resource - nexaa"
subcategory: ""
description: |-
  Cloud Database Cluster resource representing a managed database cluster on Nexaa.
---

# nexaa_cloud_database_cluster (Resource)

Cloud Database Cluster resource representing a managed database cluster on Nexaa.

## Example Usage

```terraform
terraform {
  required_version = ">= 1.0"
  required_providers {
    nexaa = {
      source = "nexaa-cloud/nexaa"
    }
  }
}

variable "nexaa_username" {
  description = "Username for Nexaa authentication"
  type        = string
  sensitive   = true
}

variable "nexaa_password" {
  description = "Password for Nexaa authentication"
  type        = string
  sensitive   = true
}

variable "namespace" {
  description = "Namespace name"
  type        = string
  sensitive   = true
}

variable "namespace_description" {
  description = "Namespace description"
  type        = string
  sensitive   = true
}

variable "cluster_name" {
  description = "Cluster name"
  type        = string
  sensitive   = true
}

variable "database_name" {
  description = "Name of user created on the cluster"
  type        = string
  sensitive   = true
}

provider "nexaa" {
  username = var.nexaa_username
  password = var.nexaa_password
}

data "nexaa_cloud_database_cluster_plans" "plan" {
  cpu      = 1
  memory   = 2.0
  storage  = 10
  replicas = 1
}

resource "nexaa_namespace" "project" {
  name        = var.namespace
  description = var.namespace_description
}

# Basic cloud database cluster
resource "nexaa_cloud_database_cluster" "cluster" {
  depends_on = [nexaa_namespace.project]
  cluster = {
    name      = var.cluster_name
    namespace = nexaa_namespace.project.name
  }

  spec = {
    type    = "PostgreSQL"
    version = "16.4"
  }

  plan = data.nexaa_cloud_database_cluster_plans.plan.id
}
```

### Variables file (variables.tfvars)
```terraform
nexaa_username = "your-nexaa-username"
nexaa_password = "your-nexaa-password"

namespace             = "project"
namespace_description = "This is a optional description for a namespace"

cluster_name = "cluster"
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `cluster` (Object) Cloud database cluster (see [below for nested schema](#nestedatt--cluster))
- `plan` (String) Plan for the cloud database cluster.
- `spec` (Object) Database specification including type and version (see [below for nested schema](#nestedatt--spec))

### Optional

- `timeouts` (Block, Optional) (see [below for nested schema](#nestedblock--timeouts))

### Read-Only

- `hostname` (String) Hostname of the cloud database cluster
- `id` (String) Unique identifier of the cloud database cluster
- `last_updated` (String) Timestamp of the last Terraform update of the cloud database cluster
- `state` (String) Current state of the cloud database cluster

<a id="nestedatt--cluster"></a>
### Nested Schema for `cluster`

Required:

- `name` (String)
- `namespace` (String)


<a id="nestedatt--spec"></a>
### Nested Schema for `spec`

Required:

- `type` (String)
- `version` (String)


<a id="nestedblock--timeouts"></a>
### Nested Schema for `timeouts`

Optional:

- `create` (String) A string that can be [parsed as a duration](https://pkg.go.dev/time#ParseDuration) consisting of numbers and unit suffixes, such as "30s" or "2h45m". Valid time units are "s" (seconds), "m" (minutes), "h" (hours).
- `delete` (String) A string that can be [parsed as a duration](https://pkg.go.dev/time#ParseDuration) consisting of numbers and unit suffixes, such as "30s" or "2h45m". Valid time units are "s" (seconds), "m" (minutes), "h" (hours). Setting a timeout for a Delete operation is only applicable if changes are saved into state before the destroy operation occurs.

## Import

Import is supported using the following syntax:

```shell
terraform import nexaa_cloud_database_cluster_database project/cluster
```

## More examples

Below there is a example with a database and a user, which shows you how to add more database and users. 
You are also able to give more permissions to a certain user.

```terraform
terraform {
  required_version = ">= 1.0"
  required_providers {
    nexaa = {
      source = "nexaa-cloud/nexaa"
    }
  }
}

variable "nexaa_username" {
  description = "Username for Nexaa authentication"
  type        = string
  sensitive   = true
}

variable "nexaa_password" {
  description = "Password for Nexaa authentication"
  type        = string
  sensitive   = true
}

variable "namespace" {
  description = "Namespace name"
  type        = string
  sensitive   = true
}

variable "namespace_description" {
  description = "Namespace description"
  type        = string
  sensitive   = true
}

variable "cluster_name" {
  description = "Cluster name"
  type        = string
  sensitive   = true
}

variable "database_name" {
  description = "Database name getting created on the cluster"
  type        = string
  sensitive   = true
}

variable "user_name" {
  description = "Name of user created on the cluster"
  type        = string
  sensitive   = true
}

variable "password" {
  sensitive = true
  type      = string
}

provider "nexaa" {
  username = var.nexaa_username
  password = var.nexaa_password
}

data "nexaa_cloud_database_cluster_plans" "plan" {
  cpu      = 1
  memory   = 2.0
  storage  = 10
  replicas = 1
}

resource "nexaa_namespace" "project" {
  name        = var.namespace
  description = var.namespace_description
}

# Basic cloud database cluster
resource "nexaa_cloud_database_cluster" "cluster" {
  depends_on = [nexaa_namespace.project]
  cluster = {
    name      = var.cluster_name
    namespace = nexaa_namespace.project.name
  }

  spec = {
    type    = "PostgreSQL"
    version = "16.4"
  }

  plan = data.nexaa_cloud_database_cluster_plans.plan.id
}

resource "nexaa_cloud_database_cluster_database" "db1" {
  depends_on  = [nexaa_cloud_database_cluster.cluster]
  name        = var.database_name
  description = ""
  cluster     = nexaa_cloud_database_cluster.cluster.cluster
}

resource "nexaa_cloud_database_cluster_user" "user" {
  depends_on = [
    nexaa_cloud_database_cluster.cluster,
  ]

  cluster  = nexaa_cloud_database_cluster.cluster.cluster
  name     = var.user_name
  password = var.password
  permissions = [
    {
      database_name = nexaa_cloud_database_cluster_database.db1.name,
      permission    = "read_write",
      state         = "present",
    }
  ]
}
```

### Variables file (variables.tfvars)
```terraform
nexaa_username = "your-nexaa-username"
nexaa_password = "your-nexaa-password"

namespace             = "project"
namespace_description = "This is a optional description for a namespace"

cluster_name  = "cluster"
database_name = "database"
user_name     = "tf-user"
```
